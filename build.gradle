plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '1.14.0' // LOCAL BUILD
}

group 'ru.sgk'
version '1.0'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation project(':chatnotes-desktop-frontend')
}
application {
    mainClass = 'ru.sgk.chatnotesdesktop.Launcher'
    applicationDefaultJvmArgs = ['-Dprism.verbose=true', '-XX:MinHeapFreeRatio=2',
                                 '-XX:MaxHeapFreeRatio=10']
}
javafx {
    version = 21
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics']

}
subprojects {
    apply plugin: 'java'
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

runtime {
    imageZip = project.file("${buildDir}/distributions/app.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.management']
    additive = true
    jpackage {
        if (javafx.platform.classifier == 'linux') {
            installerType = 'deb'
        }
        if (javafx.platform.classifier == 'win') {
            installerType = 'exe'
            installerOptions = ['--win-dir-chooser', '--win-menu', '--win-shortcut-prompt']
        }
    }
}
runtimeZip {
    group = 'distribution'
}