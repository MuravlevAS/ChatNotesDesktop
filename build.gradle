plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '1.13.1'
}

group 'ru.sgk'
version '1.0'

repositories {
    mavenCentral()
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation project(':chatnotes-desktop-frontend')
}
application {
    mainClass = 'ru.sgk.chatnotesdesktop.Launcher'
}
javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}
subprojects {
    apply plugin: 'java'
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

runtime {
    imageZip = project.file("${buildDir}/distributions/app.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    jpackage {
        if (javafx.platform.classifier == 'linux') {
            installerType = 'deb'
        }
        if (javafx.platform.classifier == 'win') {
            installerType = 'exe'
            installerOptions = ['--win-dir-chooser', '--win-menu', '--win-shortcut-prompt']
        }
    }
}
runtimeZip {
    group = 'distribution'
}